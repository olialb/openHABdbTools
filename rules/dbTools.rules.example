//
// Rules for openHABdbTools
// Oliver Albold (2019)
//

var String lastLocationOliver = "0"
val Number AccuracyTheshhold = 100

/* store location in string for later processing */
rule "Store location"
when
Item locationOliver received update
then
	var Number accuracy = (accuracyOliver.state as QuantityType<Number>).doubleValue
	// Store only different values with a good accuracy!
	if( (lastLocationOliver != locationOliver.state.toString) && (accuracy < AccuracyTheshhold) ) {
		locationOliverString.postUpdate(locationOliver.state.toString)
		lastLocationOliver = locationOliver.state.toString
	}
end

rule "dbtools every day job"
when 
  	Time cron "0 0 6 * * ?"   // Every day 6:00 hours
then
    var String result = executeCommandLine("/etc/openhab2/scripts/dbsize.py",10000)
    logInfo( "DbTools daily", result)

    result = executeCommandLine("/etc/openhab2/scripts/dbtracks.py",10000)
    if(result != "") {
	    logInfo( "DbTools daily", result)
    }
    result = executeCommandLine("/etc/openhab2/scripts/dbtimescheet.py",10000)
    if (result != "") {
	    logInfo( "DbTools daily", result)
    }
end

rule "DBMaintenance"
when 
	Time cron "0 0 0 1 * ?"   // Frist day of a month 0:00 am
then
    val String result = executeCommandLine("/etc/openhab2/scripts/dbmaintanence.py",30000)
    logInfo( "DBTools monthly", result)
end


